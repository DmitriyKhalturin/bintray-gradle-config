apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"
apply plugin: "com.github.dcendents.android-maven"

group = publishedGroupId
version = libraryVersion


install {
  repositories.mavenInstaller {
    pom {
      project {
        packaging "aar"
        groupId publishedGroupId
        artifactId artifact

        description libraryDescription
      }
    }
  }
}


task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  archiveClassifier.set("sources")
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier.set("javadoc")
  from javadoc.destinationDir
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      pom.setPackaging("aar")
      pom.withXml {
        def dependenciesNode = asNode().appendNode("dependencies")
        // Iterate over the implementation dependencies (we don"t want the test ones), adding a <dependency> node for each
        configurations.implementation.allDependencies.each {
          // Ensure dependencies such as fileTree are not included.
          if (it.name != "unspecified") {
            def dependencyNode = dependenciesNode.appendNode("dependency")
            dependencyNode.appendNode("groupId", it.group)
            dependencyNode.appendNode("artifactId", it.name)
            dependencyNode.appendNode("version", it.version)
          }
        }

        asNode().appendNode("url", siteUrl)

        def licensesNode = asNode().appendNode("licenses")
        def licenseNode = licensesNode.appendNode("license")
        licenseNode.appendNode("name", licenseName)
        licenseNode.appendNode("url", licenseUrl)

        def scmNode = asNode().appendNode("scm")
        scmNode.appendNode("connection", gitUrl)
        scmNode.appendNode("developerConnection", gitUrl)
        scmNode.appendNode("url", siteUrl)
      }
    }
  }
}

bintray {
  Properties properties = new Properties()
  File localPropertiesFile = project.rootProject.file("local.properties")
  if (localPropertiesFile.exists()) {
    properties.load(localPropertiesFile.newDataInputStream())
  }

  user = properties.getProperty("bintray.user")
  key = properties.getProperty("bintray.apikey")

  configurations = ["archives"]
  publications = ["MyPublication"]

  pkg {
    repo = bintrayRepo
    name = bintrayName

    desc = libraryDescription

    websiteUrl = siteUrl
    issueTrackerUrl = issuesUrl
    vcsUrl = gitUrl

    licenses = allLicenses
    publish = true
    publicDownloadNumbers = true
    override = true

    version {
      desc = libraryDescription
      gpg {
        sign = true
        passphrase = properties.getProperty("bintray.gpg.password")
      }
    }
  }
}
